apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    def demoCost = '"nothing"'
    def paidCost = '"100$"'
    defaultConfig {
        applicationId "by.grodno.zagart.observer.observerandroid"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def projectName = getParent().getName()
            def fileName = projectName
            def flavor = variant.getFlavorName()
            def build = variant.getBuildType().getName()
            flavor = flavor.substring(0, 1).toUpperCase() + flavor.substring(1);
            build = build.substring(0, 1).toUpperCase() + build.substring(1);
            fileName = fileName + flavor + build + "_" + versionName + ".apk"
            output.outputFile = new File(file.parent, fileName)
            //apk name result sample: observer-androidDemoRussiaDebug_1.0.apk
        }
    }
    flavorDimensions "contentType", "country"
    productFlavors {
        demo {
            dimension "contentType"
            buildConfigField "String", "APP_COST", demoCost
        }
        paid {
            dimension "contentType"
            buildConfigField "String", "APP_COST", paidCost
        }
        usa {
            applicationId "by.grodno.zagart.observer.observerandroid.usa"
            dimension "country"
        }
        russia {
            applicationId "by.grodno.zagart.observer.observerandroid.russia"
            dimension "country"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:support-vector-drawable:24.2.1'
    compile 'com.android.support:percent:24.2.1'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.1.2'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.6.2'
    compile project(path: ':backend', configuration: 'android-endpoints')
}